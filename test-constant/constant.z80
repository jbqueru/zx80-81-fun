; Copyright (c) 2023 Jean-Baptiste M. "JBQ" "Djaybee" Queru <jbqueru@gmail.com>
; SPDX-License-Identifier: MIT

	org	$4000

frame:	LD	A,$FE		; 7
	IN	A,($FE)		; 11

				; block 18

	LD	B,95		; 7
invbl:	DJNZ	invbl		; 94*13+8 = 1230
	RET	NZ		; 5
	OUT	($FF),A		; 11

				; block 1253
				; total 1271

	LD	B,227		; 7
top:	EX	(SP),HL		; 19
	EX	(SP),HL		; 19
	DJNZ	top		; 13/8 226*51+46 = 11572
				; block 11579
				; total 12850


	LD	B,192		; 7
dloop:	CALL	line+$8000	; 155
	PUSH	HL		; 11
	INC	HL		; 6
	INC	HL		; 6
	INC	HL		; 6
	POP	HL		; 10
	DJNZ	dloop		; 13/8 191*207+202

				; block 39746
				; total 52596

	LD	B,226		; 7

bottom:	EX	(SP),HL		; 19
	EX	(SP),HL		; 19
	DJNZ	bottom		; 13/8 225*51+46 = 11521

	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	JP	frame		; 10

				; block 11574

line:	ds	32,2		; 32*4=128, executed as NOP
	RET			; 10

				; block 138