; Copyright (c) 2023 Jean-Baptiste M. "JBQ" "Djaybee" Queru <jbqueru@gmail.com>
; SPDX-License-Identifier: MIT

	org	$4000

frame:	LD	A,$FE		; 7
	IN	A,($FE)		; 11

				; block 18

	LD	B,95		; 7
invbl:	DJNZ	invbl		; 94*13+8 = 1230
	RET	NZ		; 5
	OUT	($FF),A		; 11

				; block 1253
				; total 1271

	LD	B,227		; 7
top:	EX	(SP),HL		; 19
	EX	(SP),HL		; 19
	DJNZ	top		; 13/8 226*51+46 = 11572
				; block 11579
				; total 12850


	LD	B,192		; 7
dloop:	CALL	line+$8000	; 17+142 = 159
	PUSH	HL		; 11
	INC	HL		; 6
	NOP			; 4
	NOP			; 4
	POP	HL		; 10
	DJNZ	dloop		; 13/8 191*207+202

				; block 39746
				; total 52596

	LD	B,226		; 7

bottom:	EX	(SP),HL		; 19
	EX	(SP),HL		; 19
	DJNZ	bottom		; 13/8 225*51+46 = 11521

	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	INC	BC		; 6
	JP	frame		; 10

				; block 11574

line:	db	$09
	ds	9,$08
	db	$2D,$2A,$31,$31,$34,$1A,$3C,$34,$37,$31,$29,$0F
	ds	9,$08
	db	$09
	db	$00
	RET			; 10

				; block 142