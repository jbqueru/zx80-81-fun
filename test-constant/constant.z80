; Copyright (c) 2023 Jean-Baptiste M. "JBQ" "Djaybee" Queru <jbqueru@gmail.com>
; SPDX-License-Identifier: MIT

	org	$4000

frame:	LD	A,$FE		; 7
	IN	A,($FE)		; 11

				; block 18

	LD	B,95		; 7
invbl:	DJNZ	invbl		; 94*13+8 = 1230
	RET	NZ		; 5
	OUT	($FF),A		; 11

				; block 1253
				; total 1271


	LD	B,30		; 7
top1:	PUSH	HL		; 11
	ADD	HL,HL		; 10
	ADD	HL,HL		; 10
	POP	HL		; 10
	DJNZ	top1		; 13/8
				; loop 54/49
				; block 54*n+2

	LD	B,181		; 7
top2:	PUSH	HL		; 11
	POP	HL		; 10
	PUSH	HL		; 11
	POP	HL		; 10
	DJNZ	top2		; 13/8
				; loop 55/50
				; block 55*n+2

				; 2 blocks 11579
				; total 12850

	LD	B,192		; 7
dloop:	CALL	line+$8000	; 17+142 = 159
	PUSH	HL		; 11
	INC	HL		; 6
	NOP			; 4
	NOP			; 4
	POP	HL		; 10
	DJNZ	dloop		; 13/8 191*207+202

				; block 39746
				; total 52596

	LD	B,45		; 7
bot1:	PUSH	HL		; 11
	ADD	HL,HL		; 10
	ADD	HL,HL		; 10
	POP	HL		; 10
	DJNZ	bot1		; 13/8
				; loop 54/49
				; block 54*n+2

	LD	B,166		; 7
bot2:	PUSH	HL		; 11
	POP	HL		; 10
	PUSH	HL		; 11
	POP	HL		; 10
	DJNZ	bot2		; 13/8
				; loop 55/50
				; block 55*n+2

				; 2 blocks 11564

	JP	frame		; 10

				; block 11574
				; total 64170

line:	db	$09
	ds	9,$08
	db	$2D,$2A,$31,$31,$34,$1A,$3C,$34,$37,$31,$29,$0F
	ds	9,$08
	db	$09
	db	$00
	RET			; 10

				; block 142